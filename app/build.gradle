/*
 * Copyright 2012-2017 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdkVersion = 25
    buildToolsVersion = androidBuildToolsVersion

    defaultConfig {
        applicationId = "org.tobi29.scapes"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode = 1
        versionName = "1.0"
        /*jackOptions {
            enabled true
        }
        dexOptions {
            javaMaxHeapSize "2048M"
        }*/
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro")
        }
        debug {
            minifyEnabled false
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro", "proguard-rules-debug.pro")
        }
    }

    lintOptions {
        abortOnError = false
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }
}

dependencies {
    compile rootProject.ext.scapesCore
    compile rootProject.ext.scapesVanillaBasics
    compile rootProject.ext.scapesEngineEngine
    compile rootProject.ext.scapesEngineFileSystems
    compile rootProject.ext.scapesEngineUtils
    compile rootProject.ext.scapesEngineCodecOGG
    compile rootProject.ext.scapesEngineCodecMP3
    compile rootProject.ext.scapesEngineCodecWAV
    compile rootProject.ext.kotlinLogging
    compile rootProject.ext.slf4jAndroid
    compile rootProject.ext.kotlinSTD

    if (findProperty("build.local.engineandroid") == null) {
        compile rootProject.ext.scapesEngineAndroidBackend
        compile rootProject.ext.scapesEngineAndroidFileSystem
        compile rootProject.ext.scapesEngineAndroidSQLite
        compile rootProject.ext.scapesEngineAndroidSSLProvider
        compile rootProject.ext.scapesEngineAndroidSysTrace
    } else {
        compile(name: "AndroidBackend-release", ext: "aar")
        compile(name: "AndroidFileSystem-release", ext: "aar")
        compile(name: "AndroidSQLite-release", ext: "aar")
        compile(name: "AndroidSSLProvider-release", ext: "aar")
        compile(name: "AndroidSysTrace-release", ext: "aar")
        compile(name: "OpenAL-release", ext: "aar")
        compile rootProject.ext.scapesEngineOpenALSounds
    }
}

configurations.all {
    // tika-core 1.13 and later crashes on Android
    // https://issues.apache.org/jira/browse/TIKA-1981
    resolutionStrategy.force "org.apache.tika:tika-core:1.14",
            "org.apache.tika:tika-core:1.12"
}
